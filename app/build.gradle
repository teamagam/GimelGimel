apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

android {

    def globalConfiguration = rootProject.ext

    compileSdkVersion globalConfiguration.androidCompileSdkVersion
    buildToolsVersion globalConfiguration.androidBuildToolsVersion

    def versionPropsFile = file('version.properties')
    def versionBuild

    /*Setting default value for versionBuild which is the last incremented value stored in the file */
    if (versionPropsFile.canRead()) {
        def Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        versionBuild = versionProps['VERSION_BUILD'].toInteger()
    } else {
        throw new GradleException("Could not read version.properties!")
    }

    /*Wrapping inside a method avoids auto incrementing on every gradle task run. Now it runs only when we build apk*/
    ext.autoIncrementBuildNumber = {

        if (versionPropsFile.canRead()) {
            def Properties versionProps = new Properties()
            versionProps.load(new FileInputStream(versionPropsFile))
            versionBuild = versionProps['VERSION_BUILD'].toInteger() + 1
            versionProps['VERSION_BUILD'] = versionBuild.toString()
            versionProps.store(versionPropsFile.newWriter(), null)
        } else {
            throw new GradleException("Could not read version.properties!")
        }
    }

    defaultConfig {
        minSdkVersion globalConfiguration.androidMinSdkVersion
        targetSdkVersion globalConfiguration.androidTargetSdkVersion
        applicationId globalConfiguration.androidApplicationId
        versionCode globalConfiguration.androidVersionCode
        versionName globalConfiguration.androidVersionName + versionBuild
    }

    signingConfigs {
        release {
            keyAlias 'ggapp'
            keyPassword 'teamagam1'
            storePassword 'teamagam1'
            storeFile file('../keystores/gimelgimel.jks')
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

        }

        debug {
            signingConfig signingConfigs.debug
            applicationIdSuffix ".debug"

            // Run code coverage reports by default on debug builds.
            testCoverageEnabled = false
        }
    }

    // Hook to check if the release/debug task is among the tasks to be executed.
    //Let's make use of it
    gradle.taskGraph.whenReady {taskGraph ->
        if (taskGraph.hasTask(assembleDebug)) {  /* when run debug task */
            autoIncrementBuildNumber()
        } else if (taskGraph.hasTask(assembleRelease)) { /* when run release task */
            autoIncrementBuildNumber()
        }
    }

    dataBinding {
        enabled = true
    }

}

dependencies {

    def dependencies = rootProject.ext.dependencies
    def testDependencies = rootProject.ext.testDependencies

    compile project(':xwalk_core_library')
    compile project(path: ':domain')
    compile project(path: ':data')

    /* android design */
    compile dependencies.recyclerview
    compile dependencies.androidDesign
    compile dependencies.slideUpPannel
    compile dependencies.androidSupportV4
    compile dependencies.pagerSlidingTabStrip

    /* rx */
    compile dependencies.rxAndroid

    /* Dagger 2 */
    compile dependencies.dagger
    apt     dependencies.daggerCompiler
    provided dependencies.javaxAnnotations
    provided dependencies.autoFactoryCompiler

    /* Butterknife */
    compile dependencies.butterKnife
    apt     dependencies.butterKnifeCompiler

    /* Fresco by Facebook */
    compile dependencies.fresco
    compile dependencies.frescoAnimatedGif

    /* log4j */
    compile dependencies.log4jLogging
    compile dependencies.log4jAppache

    /* Test */
    testCompile testDependencies.junit
    testCompile testDependencies.roboelectric
    testCompile testDependencies.mockito

    /* Android Test */
    androidTestCompile testDependencies.robotium
}

android {
// Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }
}


